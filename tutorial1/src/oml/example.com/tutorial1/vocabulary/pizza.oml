@dc:description "A vocabulary about pizzas"
vocabulary <http://example.com/tutorial1/vocabulary/pizza#> as pizza {
	
	extends <http://www.w3.org/2001/XMLSchema#> as xsd

	extends <http://purl.org/dc/elements/1.1/> as dc

	extends <http://www.w3.org/2000/01/rdf-schema#> as rdfs

	// Top Level

	@rdfs:comment "The class of things that are uniquely identified by id"
	aspect IdentifiedThing [
		key hasId
	]
	
	@rdfs:comment "The id property of an identified thing"
	scalar property hasId [
		domain IdentifiedThing
		range xsd:string
		functional
	]	

	// Identified Things

	@rdfs:comment "The class of food items"
	concept Food < IdentifiedThing

	@rdfs:comment "A relation from a food to another used as an ingredient"
	relation entity HasIngredient [
		from Food
		to Food
		forward hasIngredient
		reverse isIngredientOf
		transitive
	]

	@rdfs:comment "An enumeration of spiciness levels"
	scalar Spiciness [
		oneOf "Hot", "Medium", "Mild"
	]

	@rdfs:comment "The spiciness property of a food item"
	scalar property hasSpiceness [
		domain Food
		range Spiciness
		functional
	]
	
	// Foods
	
	@rdfs:comment "The class of pizzas"
	concept Pizza < Food [
		restricts some hasBase to PizzaBase
	]

	@rdfs:comment "The class of pizza bases"
	concept PizzaBase < Food

	@rdfs:comment "The class of pizza toppings"
	concept PizzaTopping < Food

	@rdfs:comment "A relation from a pizza to a base"
	relation entity HasBase [
		from Pizza
		to PizzaBase
		forward hasBase
		reverse isBaseOf
		functional
		inverse functional
	] < HasIngredient 

	@rdfs:comment "A relation from a pizza to a topping"
	relation entity HasTopping [
		from Pizza
		to PizzaTopping
		forward hasTopping
		reverse isToppingOf
		inverse functional
	] < HasIngredient

	// Pizzas
		
	@rdfs:comment "The class of pizzas with some cheese toppings"
	concept CheesyPizza < Pizza [
		restricts some hasTopping to CheeseTopping
	]
	
	@rdfs:comment "The class of pizzas with some meat toppings"
	concept MeatyPizza < Pizza [
		restricts some hasTopping to MeatTopping
	]
	
	@rdfs:comment "The class of pizzas with all vegetarian toppings"
	concept VegetarianPizza < Pizza [
		restricts all hasTopping to VegetarianTopping
	]

	@rdfs:comment "The class of American pizzas"
	concept American < CheesyPizza, MeatyPizza [
		restricts some hasTopping to MozzarellaTopping
		restricts some hasTopping to SausageTopping
		restricts some hasTopping to TomatoTopping
	]

	@rdfs:comment "The class of Veneziana pizzas"
	concept Veneziana < CheesyPizza, MeatyPizza [
		restricts some hasTopping to MozzarellaTopping
		restricts some hasTopping to TomatoTopping
		restricts some hasTopping to SultanaTopping
	]

	@rdfs:comment "The class of Margherita pizzas"
	concept Margherita < CheesyPizza, VegetarianPizza [
		restricts some hasTopping to MozzarellaTopping
		restricts some hasTopping to TomatoTopping
	]

	// Pizza Bases

	@rdfs:comment "The class of deep pan bases"
	concept DeepPanBase < PizzaBase
	
	@rdfs:comment "The class of thin and crispy bases"
	concept ThinAndCrispyBase < PizzaBase

	// Pizza Toppings
		
	@rdfs:comment "The class of meat toppings"
	concept MeatTopping < PizzaTopping
	
	@rdfs:comment "The class of vegetarian toppings"
	concept VegetarianTopping < PizzaTopping

	@rdfs:comment "The class of hot spiciness toppings"
	concept HotTopping < PizzaTopping [
		restricts hasSpiceness to "Hot"
	]

	@rdfs:comment "The class of medium spiciness toppings"
	concept MediumTopping < PizzaTopping [
		restricts hasSpiceness to "Medium"
	]

	@rdfs:comment "The class of mild spiciness toppings"
	concept MildTopping < PizzaTopping [
		restricts hasSpiceness to "Mild"
	]

	// Meat Topping

	@rdfs:comment "The class sausage toppings"
	concept SausageTopping < MeatTopping, MildTopping
	@rdfs:comment "The class spiced beef toppings"
	concept SpicedBeefTopping < MeatTopping, HotTopping
	
	// Vegetarion Toppings

	@rdfs:comment "The class sauce toppings"
	concept SauceTopping < VegetarianTopping
	@rdfs:comment "The class cheese toppings"
	concept CheeseTopping < VegetarianTopping
	@rdfs:comment "The class fruit toppings"
	concept FruitTopping < VegetarianTopping
	@rdfs:comment "The class vegetable toppings"
	concept VegetableTopping < VegetarianTopping

	// Sauce Toppings

	@rdfs:comment "The class of tabasco toppings"
	concept TobascoTopping < SauceTopping, HotTopping

	// Cheese Toppings

	@rdfs:comment "The class of parmesan toppings"
	concept ParmesanTopping < CheeseTopping, MildTopping
	@rdfs:comment "The class of mozzarella toppings"
	concept MozzarellaTopping < CheeseTopping, MildTopping

	// Fruit Toppings

	@rdfs:comment "The class of sultana toppings"
	concept SultanaTopping < FruitTopping, MediumTopping
	
	// Vegetable Toppings

	@rdfs:comment "The class of pepper toppings"
	concept PepperTopping < VegetableTopping
	@rdfs:comment "The class of tomatoe toppings"
	concept TomatoTopping < VegetableTopping, MildTopping

	// Pepper Toppings

	@rdfs:comment "The class of jalapeno pepper toppings"
	concept JalapenoPepperTopping < PepperTopping, HotTopping
	@rdfs:comment "The class of sweet pepper toppings"
	concept SweetPepperTopping < PepperTopping, MildTopping
}